def workingDirs

pipeline {
  agent { label 'ecs-rockylinux8_openjdk17_jenkins_agent_all' }
  options {
    checkoutToSubdirectory('tf_terragrunt')
    ansiColor('xterm')
  }
  triggers {
    cron('TZ=America/New_York\n30 3 * * *') // Every night at 3:30 AM Eastern
  }
  stages {
    stage('Get Terragrunt modules') {
      steps {
        script {
          workingDirs = sh(
            returnStdout: true,
            script: '''
              cd tf_terragrunt && find . -type f \
                \\( -name "aws_iam.tfvars" -o -name "azure_app_registration.tfvars" \\) \
                -not -path "*/.terragrunt-cache/*" \
                -exec grep -q "auto_rotate_days" {} \\; \
                -printf "%h\\n"
            '''
          ).trim().tokenize()
          echo "Found ${workingDirs.size()} Terragrunt modules: ${workingDirs}"
        }
      }
    }
    stage('Build Terragrunt modules') {
      steps {
        script {
          for (workingDir in workingDirs) {
            def parameters = [
              booleanParam(name: 'RETRY_PIPELINE', value: true),
              string(name: 'WORKING_DIR', value: workingDir)
            ]
            build job: '/terraform/terragrunt-apply-prod', parameters: parameters,
              propagate: false, wait: false
          }
        }
      }
    }
  }
}
